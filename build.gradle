plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'dev.abbysrc'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.essentialsx.net/releases/" }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly 'net.essentialsx:EssentialsX:2.19.4'
    implementation "net.kyori:adventure-text-serializer-legacy:4.11.0"
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'org.reflections:reflections:0.9.9-RC1'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

shadowJar {
    relocate 'co.aikar.commands', 'dev.abbysrc.commands.framework'
    relocate 'co.aikar.locales', 'dev.abbysrc.commands.framework.locales'
}

java {
    def javaVersion = JavaVersion.toVersion(17)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isJava10Compatible()) {
        options.release.set(17)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
